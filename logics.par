// Logics: A Python-like evaluation language
// This grammar requires unicc >= 1.9 to compile, use the Makefile for building.

%!mode          scanner;
%prefix         "Logics";
%whitespaces    @whitespace;

@whitespace     ' \r\n\t'+
                | "#" !'\n'* '\n'
                ;

@Identifier     'A-Za-z_' 'A-Za-z0-9_'*                             = Identifier;
@Number         '0-9'+ '.' '0-9'* | '0-9'* '.' '0-9'+ | '0-9'+      = Number;
@String         '"' ( '\\' . | !'\\"' )* '"'
                | '\'' ( '\\' . | !'\\\'' )* '\''                   = String;


expression$     : or "if" expression "else" expression              = if
                | or
                ;

or              : or "or" and                                       = or
                | and
                ;

and             : and "and" not                                     = and
                | not
                ;

not             : "not" not                                         = not
                | cmp
                ;

cmp             : add_sub (
                        "==" add_sub                                    = eq
                        | ">" add_sub                                   = gt
                        | ">=" add_sub                                  = gteq
                        | "<" add_sub                                   = lt
                        | "<=" add_sub                                  = lteq
                        | ("!=" | "<>") add_sub                         = neq
                        | "in" add_sub                                  = in
                        | "not" "in" add_sub                            = outer
                    )+                                              = cmp
                | add_sub
                ;

add_sub         : add_sub '+' mul_div                               = add
                | add_sub '-' mul_div                               = sub
                | mul_div
                ;

mul_div         : mul_div '*' unary                                 = mul
                | mul_div '/' unary                                 = div
                | mul_div "//" unary                                = idiv
                | mul_div '%' unary                                 = mod
                | pow;

pow             : pow "**" unary                                    = pow
                | unary
                ;

unary           : '+' unary                                         = pos
                | '-' unary                                         = neg
                | '~' unary                                         = invert
                | factor
                ;

factor          : atom
                | atom trailer+                                     = entity
                | @Identifier '(' list? ')'                         = call
                ;

opt_expression  : expression
                |                                                   = None
                ;

trailer         : '[' expression ']'                                = index
                | '[' opt_expression ':' opt_expression ']'         = slice
                | '.' @Identifier                                   = attr
                ;

atom            : ""True""
                | ""False""
                | ""None""
                | "$"                                               = vars
                | @Number
                | @Identifier                                       = load
                | @String
                | @String+                                          = strings
                | '[' expression "for" @Identifier "in"
                    or ( "if" expression )? ']'                     = comprehension
                | '[' list ']'
                | '(' expression ',' ')'                            = list
                | '(' expression ',' internal_list ','? ')'         = list
                | '(' expression ')'
                ;

internal_list   : expression
                | internal_list ',' expression
                ;

list            := internal_list ','?
                ;
